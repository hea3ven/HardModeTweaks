buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

repositories {
    maven {
        url "https://raw.github.com/hea3ven/mvn-repo/master"
    }
}

configurations {
    release
}

apply plugin: 'forge'

sourceCompatibility = 1.7
targetCompatibility = 1.7

version = "1.8-1.1.3"
group = "com.hea3ven"
archivesBaseName = "hardmodetweaks"

minecraft {
    version = "1.8-11.14.3.1500"
    runDir = "eclipse/"
    replaceIn "src/com/hea3ven/hardmodetweaks/config/ProjectConfig.java"
    replace "PROJECTVERSION", project.version
    replace "MCVERSION", project.minecraft.version
    replace "FORGEVERSION", project.minecraft.apiVersion.split('-')[1]
}

dependencies {
    compile 'com.hea3ven.tools.asmtweaks:asmtweaks:1.0.0'
    release('com.hea3ven.tools.asmtweaks:asmtweaks:1.0.0') {
        transitive = false
    }
    release('com.hea3ven.tools.mappings:mappings:1.0.0') {
        transitive = false
    }
}


jar {
    manifest {
        attributes("FMLCorePlugin":"com.hea3ven.hardmodetweaks.core.LoadingPluginHardModeTweaks")
        attributes("FMLCorePluginContainsFMLMod":"true")
    }

    into ('libs') {
        from configurations.release
    }
}

sourceSets.main {
    java {
        srcDirs 'src'
        srcDirs 'Hea3venTweaks/src/main/java'
        srcDirs 'Hea3venTweaks/src/bootstrap/java'
    }
    resources {
        srcDirs 'resources'
        srcDirs 'Hea3venTweaks/src/main/resources'
        srcDirs 'Hea3venTweaks/src/bootstrap/resources'
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    inputs.property "forgeversion", project.minecraft.apiVersion

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        include 'project.properties'
        expand 'version':project.version, 'mcversion':project.minecraft.version, 'forgeversion':project.minecraft.apiVersion
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
        exclude 'project.properties'
    }
}

